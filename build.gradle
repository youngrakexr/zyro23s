buildscript {
	ext {
		grailsVersion = "3.0.10"
		springVersion = "4.1.8.RELEASE"
	}
	repositories {
		mavenLocal()
		maven { url "http://repo.grails.org/grails/core" }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsVersion"
	}
}

plugins {
	id "io.spring.dependency-management" version "0.5.4.RELEASE"
	id "com.jfrog.bintray" version "1.5"
}

version "2.2.0"
group "org.grails.plugins"

apply plugin: "maven-publish"
apply plugin: "eclipse"
apply plugin: "spring-boot"
apply plugin: "org.grails.grails-plugin"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "http://repo.grails.org/grails/core" }
}

dependencyManagement {
	imports {
		mavenBom "org.grails:grails-bom:$grailsVersion"
	}
	applyMavenExclusions false
}

dependencies {
	provided "javax.servlet:javax.servlet-api:3.1.0"
	provided "org.grails:grails-dependencies"
	provided "org.grails:grails-web-boot"
	provided "org.springframework.boot:spring-boot-starter-logging"
	provided "org.springframework.boot:spring-boot-starter-actuator"
	provided "org.springframework.boot:spring-boot-autoconfigure"
	provided "org.springframework.boot:spring-boot-starter-tomcat"
	
	compile "com.fasterxml.jackson.core:jackson-databind:2.6.3"
	compile "org.springframework:spring-messaging:${springVersion}"
	compile "org.springframework:spring-websocket:${springVersion}"
	
	testCompile "org.grails:grails-plugin-testing"
	
	console "org.grails:grails-console"
}

publishing {
	publications {
		maven(MavenPublication) {
			pom.withXml {
				def pomNode = asNode()
				pomNode.dependencyManagement.replaceNode {}
				pomNode.dependencies.dependency.findAll {
					!it.version.text()
				}*.replaceNode {}
			}
			artifactId project.name
			from components.java
			artifact sourcesJar
			artifact javadocJar
			artifact source: "${sourceSets.main.output.classesDir}/META-INF/grails-plugin.xml", classifier: "plugin", extension: "xml"
		}
	}
}

bintray {
	user = project.hasProperty("bintrayUser") ? project.bintrayUser : ""
	key = project.hasProperty("bintrayKey") ? project.bintrayKey : ""
	publications = ["maven"]
	publish = true
	pkg {
		repo = "maven"
		name = project.name
	}
}

eclipse {
	project {
		buildCommand "org.springframework.ide.eclipse.core.springbuilder"
		natures "org.springframework.ide.eclipse.core.springnature"
	}
}

wrapper.gradleVersion = "2.9"
