buildscript {
	ext {
		grailsVersion = "3.0.0.RC3"
		springVersion = "4.1.6.RELEASE"
	}
	repositories {
		mavenLocal()
		maven { url "http://repo.grails.org/grails/core" }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsVersion"
	}
}

plugins {
	id "io.spring.dependency-management" version "0.4.0.RELEASE"
	id "com.jfrog.bintray" version "1.1"
}

version "2.0.0.RC1"
group "org.grails.plugins"

apply plugin: "maven-publish"
apply plugin: "eclipse"
apply plugin: "spring-boot"
apply plugin: "org.grails.grails-plugin"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "http://repo.grails.org/grails/core" }
}

dependencyManagement {
	imports {
		mavenBom "org.grails:grails-bom:$grailsVersion"
	}
}

dependencies {
	provided "javax.servlet:javax.servlet-api:3.1.0"
	provided "org.grails:grails-dependencies"
	provided "org.grails:grails-web-boot"
	provided "org.springframework.boot:spring-boot-starter-logging"
	provided "org.springframework.boot:spring-boot-starter-actuator"
	provided "org.springframework.boot:spring-boot-autoconfigure"
	provided "org.springframework.boot:spring-boot-starter-tomcat"
	
	compile "com.fasterxml.jackson.core:jackson-databind:2.5.1"
	compile "org.springframework:spring-messaging:${springVersion}"
	compile "org.springframework:spring-websocket:${springVersion}"
	
	testCompile "org.grails:grails-plugin-testing"
	
	console "org.grails:grails-console"
}

publishing {
	publications {
		maven(MavenPublication) {
			pom.withXml {
				def pomNode = asNode()
				pomNode.dependencyManagement.replaceNode {}
				pomNode.dependencies.dependency.findAll {
					it.version.text().isEmpty()
				}.each {
					it.replaceNode {}
				}
			}
			artifactId project.name
			from components.java
			artifact sourcesJar
			artifact javadocJar
			artifact source: "${sourceSets.main.output.classesDir}/META-INF/grails-plugin.xml", classifier: "plugin", extension: "xml"
		}
	}
}

bintray {
	user = project.hasProperty("bintrayUser") ? project.bintrayUser : ""
	key = project.hasProperty("bintrayKey") ? project.bintrayKey : ""
	publications = ["maven"]
	publish = true
	pkg {
		repo = "maven"
		name = project.name
	}
}

task install(dependsOn: project.tasks.withType(PublishToMavenLocal))

task wrapper(type: Wrapper) {
	gradleVersion = "2.3"
}
